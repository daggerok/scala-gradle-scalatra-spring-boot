/*
    gradle clean build
    gradle app
*/

buildscript {
    repositories { jcenter() }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVer",
                  "com.github.ben-manes:gradle-versions-plugin:$gradleVersionsPluginVer"
    }
}

plugins {
    id "io.spring.dependency-management" version '0.5.6.RELEASE'
    id 'net.saliman.properties' version '1.4.5'
}

apply plugin: 'scala'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'com.github.ben-manes.versions'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = sourceCompatibility

group = group

jar {
    version = version
    baseName = baseName
}

repositories { jcenter() }

// gradle bootRun -Dserver.context-path=/app
bootRun { systemProperties = System.properties }

dependencies {
    compile "org.scala-lang:scala-library:$scalaVer",
            "org.slf4j:slf4j-api:$slf4jVer",
            "org.psnively:spring_scala_$springScalaVer",
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-devtools',
            "org.scalatra:scalatra_$scalatraVer",
            "org.scalatra:scalatra-json_$scalatraVer",
            "org.scalatra:scalatra-json:$scalatraJsonVer",
            "org.json4s:json4s-jackson_$json4sJacksonVer"

    testCompile "junit:junit:$jUnitVer",
                "org.scalactic:scalactic_$scalatestVer",
                "org.scalatest:scalatest_$scalatestVer",
                "org.specs2:specs2_$specs2Ver",
                'org.springframework.boot:spring-boot-starter-test',
                "org.scalatra:scalatra-scalatest_$scalatraVer",
                "org.scalatra:scalatra-specs2_$scalatraVer"
}

task app(type: JavaExec, dependsOn: classes) {
    main = 'com.daggerok.scala.spring.Application'
    classpath sourceSets.main.runtimeClasspath,
              configurations.runtime
}

tasks.withType(ScalaCompile) {
    configure(scalaCompileOptions.forkOptions) {
        memoryMaximumSize = '1g'
        jvmArgs = ['-XX:MaxPermSize=512m']
    }
}

task wrapper(type: Wrapper) { gradleVersion = gradleVersion }
